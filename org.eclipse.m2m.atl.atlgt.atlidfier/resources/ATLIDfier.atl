-- @atlcompiler emftvm

-- @path ATL=/ATLIDfier/ATL.ecore

module ATLIDfier;
create OUT : ATL refining IN : ATL;

-- 'IN'.concat(s.__xmiID__.concat('.Class2Table.t')),
rule OutPatternElement {
		from
			s : ATL!OutPatternElement
		to
			t : ATL!OutPatternElement (
				bindings <- s.bindings->including(xmiIdBinding)
			),
			xmiIdBinding : ATL!Binding (
				propertyName <- '__xmiID__',
				value <- xmiIdExpr
			),			
			xmiIdExpr : ATL!OperationCallExp (
				source <- inExpr,
				operationName <- 'concat',
				arguments <- infileExpr
			),
			inExpr : ATL!StringExp (
				stringSymbol <- 'IN'
			),
			infileExpr : ATL!OperationCallExp (
				source <- oldXmiIdExpr,
				operationName <- 'concat',
				arguments <- bindingNameExpr
			),
			oldXmiIdExpr : ATL!NavigationOrAttributeCallExp (
				source <- varNameExpr,
				name <- '__xmiID__'
			),
			varNameExpr : ATL!VariableExp (
				referredVariable <- s.outPattern.rule.inPattern.elements->first()
			),
			bindingNameExpr : ATL!StringExp (
				stringSymbol <- '.'.concat(s.outPattern.rule.name).concat('.').concat(s.varName)
			)
}